package SAT_system
public
	with Buses::I2C, Buses::UART, Buses::SPI, Buses::Radio, Buses::Analog;
	with SAT_EPS_devices, SAT_EPS_process;
	with SAT_OBDH_devices, SAT_OBDH_process;
	with SAT_Payloads;
	with SAT_TTC_devices;
	with common_devices;
system SAT1
end SAT1;

system implementation SAT1.impl
	subcomponents
------------ EPS DEVICES ---------------------------
		solar_panel_ym: device SAT_EPS_devices::Solar_Panel;
		solar_panel_xp: device SAT_EPS_devices::Solar_Panel;
		solar_panel_xm: device SAT_EPS_devices::Solar_Panel;
		solar_panel_zp: device SAT_EPS_devices::Solar_Panel;
		solar_panel_zm: device SAT_EPS_devices::Solar_Panel;
		solar_panel_yp: device SAT_EPS_devices::Solar_Panel;
	-- Current sensor for each Solar Panel
		sens_I_sp_ym: device common_devices::MAX9934TAUA;
		sens_I_sp_xp: device common_devices::MAX9934TAUA;
		sens_I_sp_xm: device common_devices::MAX9934TAUA;
		sens_I_sp_zp: device common_devices::MAX9934TAUA;
		sens_I_sp_zm: device common_devices::MAX9934TAUA;
		sens_I_sp_yp: device common_devices::MAX9934TAUA;
	-- Voltage Sensor for two parallel Solar Panels
		sens_V_sp_1: device common_devices::TLV341AIDBVR;
		sens_V_sp_2: device common_devices::TLV341AIDBVR;
		sens_V_sp_3: device common_devices::TLV341AIDBVR;
	-- MPPT Solar Panels controller
		mppt_1: device SAT_EPS_devices::mppt;
		mppt_2: device SAT_EPS_devices::mppt;
		mppt_3: device SAT_EPS_devices::mppt;
	-- Board temperature sensor
		temp_sensor: device SAT_EPS_devices::ADS1248IPW;
	-- Heat driver controlling board and battery temperature
		heat_driver: device SAT_EPS_devices::heat_driver;
	-- Battery Supply
		battery: device SAT_EPS_devices::battery;
		battery_charge: device SAT_EPS_devices::battery_charger;
		battery_monitor: device SAT_EPS_devices::battery_monitor;
	-- LMC555 Clock
		lmc555: device SAT_EPS_devices::LMC555;
	-- Beacon current sensor
	beacon_current: device common_devices::MAX9934TAUA;
	-- Vbus voltage sensor
	vbus_voltage: device common_devices::TLV341AIDBVR;
	-- Solar Panels voltage measurement
	SP_Total_V: device common_devices::TLV341AIDBVR;
	-- DC-DC EPS_Beacon
	DC_EPS_Beacon: device SAT_EPS_devices::TPS5420;
	-- DC-DC Antenna Deployment
	DC_Antenna: device SAT_EPS_devices::TPS5420;
	-- DC-DC OBDH/TTC
	DC_OBDH_TTC: device SAT_EPS_devices::TPS5410;
	-- DC-DC PA TTC
	DC_PA_TTC: device SAT_EPS_devices::TPS54540;
	-- DC-DC PA Beacon
	DC_PA_Beacon: device SAT_EPS_devices::TPS54540;
	-- DC-DC Payloads
	DC_Payloads: device SAT_EPS_devices::TPS5430;

	
	-- MCU of EPS
		mcu_eps: processor common_devices::MSP430F6659;
		EPS_firmware: process SAT_EPS_process::EPS_firmware.impl;
		
------------ TTC DEVICES -----------------------------------
		beacon_radio: device SAT_TTC_devices::beacon_radio;
		mcu_ttc: processor common_devices::MSP430F6659.impl;
		ttc_main_radio : device SAT_TTC_devices::main_radio;
		mcu_antenna: processor common_devices::MSP430F6659.impl;
------------ OBDH DEVICES -----------------------------------
		mcu_obdh: processor common_devices::MSP430F6659.impl;
		imu_1: device SAT_OBDH_devices::MPU9250;
		imu_2: device SAT_OBDH_devices::BMX055;
		voltage_monitor: device SAT_OBDH_devices::voltage_monitor;
		current_driver_1: device SAT_OBDH_devices::DRV8833;
		current_driver_2: device SAT_OBDH_devices::DRV8833;
		current_driver_3: device SAT_OBDH_devices::DRV8833;
		current_sensor_board: device common_devices::MAX9934TAUA;
		current_sensor_1: device common_devices::MAX9934TAUA;
		current_sensor_2: device common_devices::MAX9934TAUA;
		current_sensor_3: device common_devices::MAX9934TAUA;
		voltage_sensor_board: device common_devices::TLV341AIDBVR;
		OBDH_firmware: process SAT_OBDH_process::mcu.impl;
		
		-- PAYLOAD DEVICES
		join_payload: processor SAT_Payloads::Joinville_Payload;
		rush_payload: processor SAT_Payloads::RUSH_Payload;
		fs_interface: processor SAT_Payloads::FS_Interface;
		
		-- BUSES
		BUS0: bus Buses::I2C::I2C.impl;
		BUS1: bus Buses::SPI::SPI.impl;
		BUS2: bus Buses::I2C::I2C.impl;
		BUS3: bus Buses::SPI::SPI.impl;
		BUS4: bus Buses::SPI::SPI.impl;
		BUS5: bus Buses::I2C::I2C.impl;
		BUS6: bus Buses::UART::UART.impl;
		BUS7: bus Buses::Radio::Radio.impl;
		BUS8:bus Buses::Analog::Analog.impl;
		BUS_JTAG: bus Buses::UART::UART.impl;
		
	connections
		-- BUS CONNECTIONS
			-- EPS
		Be1: bus access mcu_eps.i2c_bus -> BUS0;
		Be2: bus access mcu_eps.uart_bus -> BUS6;
		Be3: bus access mcu_eps.uart_bus -> BUS_JTAG;
		
			-- SOLAR PANELS
--		Bs1: bus access solar_panels_1.analog_bus -> BUS8;
--		Bs2: bus access solar_panels_2.analog_bus -> BUS8;
--		Bs3: bus access solar_panels_3.analog_bus -> BUS8;
--		Bs4: bus access solar_panels_1.spi_bus -> BUS1;
--		Bs5: bus access solar_panels_2.spi_bus -> BUS1;
--		Bs6: bus access solar_panels_3.spi_bus -> BUS1;
		
			-- TTC
		Bt1: bus access mcu_ttc.i2c_bus -> BUS5;
--		Bt2: bus access mcu_ttc.radio_bus -> BUS7;
		Bt3: bus access mcu_ttc.uart_bus -> BUS6; 
		Bt4: bus access mcu_ttc.uart_bus -> BUS_JTAG; 
		Bt5: bus access mcu_ttc.spi_bus -> BUS3;
			-- ANTENNA MODULE
--		Ba1: bus access mcu_antenna.radio_bus -> BUS7;
		Ba2: bus access mcu_antenna.i2c_bus -> BUS2;
		Ba3: bus access mcu_antenna.i2c_bus -> BUS5;
			-- OBDH
		Bo1: bus access  mcu_obdh.spi_bus -> BUS1;
		Bo2: bus access  mcu_obdh.spi_bus -> BUS3;
		Bo3: bus access  mcu_obdh.spi_bus -> BUS4;
		Bo4: bus access mcu_obdh.i2c_bus -> BUS0;
		Bo5: bus access mcu_obdh.i2c_bus -> BUS2;
		Bo6: bus access mcu_obdh.uart_bus -> BUS_JTAG;
--		Bo7: bus access mcu_obdh.analog_bus -> BUS8;

			-- PAYLOAD_
		Bp1: bus access join_payload.i2c_bus -> BUS0;
		Bp2: bus access rush_payload.i2c_bus -> BUS0;
		Bp3: bus access fs_interface.uart_bus -> BUS_JTAG;
		
------ EPS CONNECTIONS
		-- Solar Panels Connections
		A1_1:  port solar_panel_xm.power -> sens_I_sp_xm.power_in; -- SP XM
		A1_11: port sens_I_sp_xm.current_data -> EPS_firmware.solar_I_xm;
		A1_12: port solar_panel_xm.power-> sens_V_sp_2.power_in;
		A1_2:  port solar_panel_ym.power -> sens_I_sp_ym.power_in; -- SP YM
		A1_21: port sens_I_sp_ym.current_data -> EPS_firmware.solar_I_ym;
		A1_22: port solar_panel_ym.power-> sens_V_sp_1.power_in;
		A1_3:  port solar_panel_zm.power -> sens_I_sp_zm.power_in; -- SP ZM
		A1_31: port sens_I_sp_zm.current_data -> EPS_firmware.solar_I_zm;
		A1_32: port solar_panel_zm.power-> sens_V_sp_3.power_in;
		A1_4:  port solar_panel_xp.power -> sens_I_sp_xp.power_in; -- SP XP
		A1_41: port sens_I_sp_xp.current_data -> EPS_firmware.solar_I_xp;
		A1_42: port solar_panel_xp.power-> sens_V_sp_1.power_in;
		A1_5:  port solar_panel_yp.power -> sens_I_sp_yp.power_in; -- SP YP
		A1_51: port sens_I_sp_xp.current_data -> EPS_firmware.solar_I_yp;
		A1_52: port solar_panel_zp.power-> sens_V_sp_1.power_in;
		A1_6:  port solar_panel_zp.power -> sens_I_sp_zp.power_in; -- SP ZP
		A1_61: port sens_I_sp_zp.current_data -> EPS_firmware.solar_I_zp;
		A1_62: port solar_panel_zp.power-> sens_V_sp_2.power_in;
		V1_1: port sens_V_sp_1.voltage_data -> EPS_firmware.solar_V_1;
		V1_2: port sens_V_sp_2.voltage_data -> EPS_firmware.solar_V_2;
		V1_3: port sens_V_sp_3.voltage_data -> EPS_firmware.solar_V_3;
		-- Solar Panels to battery monitor and charger
		O1_1: port mppt_1.power_out -> battery_monitor.solar_power; -- Solar Panels Power
		O1_2: port mppt_2.power_out -> battery_monitor.solar_power;
		O1_3: port mppt_3.power_out -> battery_monitor.solar_power;
		-- Battery charge connection
		O1_7: port battery.power_out -> battery_monitor.battery_power; -- discharge battery to vBus
		O2_1: port battery_charge.power_out -> battery.power_in; -- charge battery from Solar Panels
		O2_2: port battery_monitor.solar_power_out -> battery_charge.power_in; -- Power from Solar Pannels;
		-- DC-DC Power
		O3_1: port battery_monitor.vbus -> DC_EPS_Beacon.power_in;
		O3_2: port battery_monitor.vbus -> DC_OBDH_TTC.power_in;
		O3_3: port battery_monitor.vbus -> DC_PA_Beacon.power_in;
		O3_4: port battery_monitor.vbus -> DC_PA_TTC.power_in;
		O3_5: port battery_monitor.vbus -> DC_Payloads.power_in;
		O3_6: port battery_monitor.vbus -> DC_Antenna.power_in;
		-- Beacon Communication ?????????
		A4: port EPS_firmware.data_beacon ->beacon_radio.eps_data_in;
		-- Board temperature read
		A5: port temp_sensor.temp_data -> EPS_firmware.temp_sensor;
		-- Battery statusW
		A6: port battery_monitor.status -> EPS_firmware.bat_status;
		-- Control Battery heater
		A7: port EPS_firmware.heat_pwm1-> heat_driver.pwm1_in;
		A8: port EPS_firmware.heat_pwm2-> heat_driver.pwm2_in;
		-- Solar Panel Power to MPPT
		A0_1: port solar_panel_ym.power -> mppt_1.power_in;
		A0_2: port solar_panel_xp.power -> mppt_1.power_in;
		A0_3: port solar_panel_xm.power -> mppt_2.power_in;
		A0_4: port solar_panel_zp.power -> mppt_2.power_in;
		A0_5: port solar_panel_zm.power -> mppt_3.power_in;
		A0_6: port solar_panel_yp.power -> mppt_3.power_in;
		-- PWM MPPT controller
		A9: port EPS_firmware.mppt_pwm1 -> mppt_1.pwm_in;
		A10: port EPS_firmware.mppt_pwm2 -> mppt_2.pwm_in;
		A11: port EPS_firmware.mppt_pwm3 -> mppt_3.pwm_in;
		A12: port EPS_firmware.en_lmc555 -> lmc555.trigger_in;
		-- LMC555 PWM to MPPT
		A13_1: port lmc555.out_data -> mppt_1.lmc555_in;
		A13_2: port lmc555.out_data -> mppt_2.lmc555_in;
		A13_3: port lmc555.out_data -> mppt_3.lmc555_in;
		-- Enable Connections
		E1_1: port EPS_firmware.En_OBDH_TTC -> DC_OBDH_TTC.enable_in;
		E1_2: port EPS_firmware.En_PA_Beacon -> DC_PA_Beacon.enable_in;
		E1_3: port EPS_firmware.En_PA_TTC -> DC_PA_TTC.enable_in;
		E1_4: port EPS_firmware.En_Payloads -> DC_Payloads.enable_in;
		-- EPS Beacon Current Measurement
		E2_1: port DC_EPS_Beacon.power_out -> beacon_current.power_in;
		E2_2: port beacon_current.current_data -> EPS_firmware.beacon_I;
		-- VBus voltage measurement
		E3_1: port battery_monitor.vbus -> vbus_voltage.power_in;
		E3_2: port vbus_voltage.voltage_data -> EPS_firmware.vbus_V_sensor;
		-- Solar Panel Voltage Measurement
		Q1_1: port mppt_1.power_out -> SP_Total_V.power_in; -- Solar Panels Power
		Q1_2: port mppt_2.power_out -> SP_Total_V.power_in;
		Q1_3: port mppt_3.power_out -> SP_Total_V.power_in;
		Q2: port SP_Total_V.voltage_data -> EPS_firmware.solar_V_total;
		-- TTC CONNECTIONS
		
------ OBDH CONNECTIONS
		-- Solar Panels Connections to current measurement
		C1_1a: port solar_panel_ym.power -> current_sensor_1.power_in;
		C1_1b: port solar_panel_xp.power -> current_sensor_1.power_in;
		C1_1c: port current_sensor_1.current_data -> OBDH_firmware.solar_panel_1_current;
		C1_2a: port solar_panel_xm.power -> current_sensor_2.power_in;
		C1_2b: port solar_panel_zp.power -> current_sensor_2.power_in;
		C1_2c: port current_sensor_2.current_data -> OBDH_firmware.solar_panel_2_current;
		C1_3a: port solar_panel_zm.power -> current_sensor_3.power_in;
		C1_3b: port solar_panel_yp.power -> current_sensor_3.power_in;
		C1_3c: port current_sensor_3.current_data -> OBDH_firmware.solar_panel_3_current;
		-- OBDH to current drive to magnetorques
		C2_1a: port OBDH_firmware.solar_panel_1_current_driver -> current_driver_1.signal_in;
		C2_1b: port current_driver_1.signal_out -> solar_panel_ym.magnetorq_in;
		C2_1bb: port current_driver_1.signal_out -> solar_panel_xp.magnetorq_in;
		C2_2a: port OBDH_firmware.solar_panel_2_current_driver -> current_driver_2.signal_in;
		C2_2b: port current_driver_2.signal_out -> solar_panel_xm.magnetorq_in;
		C2_2bb: port current_driver_2.signal_out -> solar_panel_zp.magnetorq_in;
		C2_3a: port OBDH_firmware.solar_panel_3_current_driver -> current_driver_3.signal_in;
		C2_3b: port current_driver_3.signal_out -> solar_panel_zm.magnetorq_in;
		C2_3bb: port current_driver_3.signal_out -> solar_panel_yp.magnetorq_in;
		-- Supply sensing
		C4_1: port current_sensor_board.current_data -> OBDH_firmware.board_current;
		C4_2: port voltage_sensor_board.voltage_data -> OBDH_firmware.board_voltage;		
		-- Voltage monitor
		C5_1: port OBDH_firmware.volt_monitor_out -> voltage_monitor.port_in;
		C5_2: port voltage_monitor.port_out -> OBDH_firmware.volt_monitor_in;
		-- IMU Data 
		C6_1: port imu_1.DOF6 -> OBDH_firmware.imu_1_data_in;
		C6_2: port imu_2.DOF6 -> OBDH_firmware.imu_2_data_in;
			
		
end SAT1.impl;
end SAT_system;