package ttc
public
	with Physical;

	--This is a composite component (it has hw and sw)
	system TTC -- Top entity
	end TTC;

	system implementation TTC.hw -- Top entity implementation
	subcomponents
		-- Parts
		mcu: processor mcu.impl;
		main_radio: device main_radio;
		beacon_radio: device beacon_radio;
		antenna_beacon: device antenna;
		antenna_main: device antenna;

		-- Buses
		bus3_ttc: bus BUS3_ttc;
		bus4_ttc: bus BUS4_ttc;
		bus5_ttc: bus BUS5_ttc;
		bus6_ttc: bus BUS6_ttc;
		bus7_beacon: bus BUS7;
		bus7_main: bus BUS7;
		mcu_beaconRadio_bus: bus generalBUS;
		debug_JTAG_bus: bus generalBUS;

		-- Headers for external connections
		pc104: device PC104;
		header: device header;
		debug_JTAG: device debug_header;

	connections
		b3_mcu: bus access bus3_ttc <-> mcu.obdh_data_exchange_B3;
		b5_mcu: bus access mcu.antenna_commands_B5 <-> bus5_ttc;
		b6_mcu: bus access mcu.eps_data_in_B6 <-> bus6_ttc;
		b3_pc104: bus access bus3_ttc <-> pc104.bus_access3;
		b4_pc104: bus access bus4_ttc <-> pc104.bus_access4;
		b6_pc104: bus access bus6_ttc <-> pc104.bus_access6;
		mcu_bus: bus access mcu.bus_access_toBeacon <-> mcu_beaconRadio_bus;
		bus_beacon: bus access mcu_beaconRadio_bus <-> beacon_radio.mcu_data_in;
		header_debugBUS: bus access debug_JTAG.bus_access <-> debug_JTAG_bus;
		debugBUS_mcu: bus access debug_JTAG_bus <-> mcu.bus_access_debug;
		bus4_mainRadio: bus access bus4_ttc <-> main_radio.obdh_radio_data;
		header_bus5: bus access header.bus_access <-> bus5_ttc;
		bus7beacon_beacon: bus access bus7_beacon <-> beacon_radio.beacon_message;
		bus7beacon_beaconAntenna: bus access bus7_beacon <-> antenna_beacon.bus_access7;
		bus7main_main: bus access bus7_main <-> main_radio.radio_signals;
		bus7main_mainAntenna: bus access bus7_main <-> antenna_main.bus_access7;
end TTC.hw;

	-- ~~~~~~~~~~ Components ~~~~~~~~~~
	---- TTC MCU	
	processor mcu
		features
			obdh_data_exchange_B3: requires bus access BUS3_ttc; -- bus3 (SPI)
			eps_data_in_B6: requires bus access BUS6_ttc; -- bus6 (UART)
			antenna_commands_B5: requires bus access BUS5_ttc; -- bus5 (I2C)
			bus_access_debug: requires bus access generalBUS;
			bus_access_toBeacon: requires bus access generalBUS;
		properties
			Clock_Period => 50ns; -- 20 MHz(max)
			Physical::Voltage => 3.6 V; --(max) 1.6 V (min)
	end mcu;

	processor implementation mcu.impl
		subcomponents
			RAM : memory mcu_RAM;
			Non_Volatile : memory mcu_nonVolatile;	
	end mcu.impl;
		--MCU memories
	memory mcu_RAM
		properties
			Memory_Size => 66 kByte;			
	end mcu_RAM;
	
	memory mcu_nonVolatile
		properties
			Memory_Size => 512 kByte;
	end mcu_nonVolatile;

	---- Main radio (transceiver)
	--(Radio Module RF4463F30)
	device main_radio
		features
			obdh_radio_data: requires bus access BUS4_ttc; -- bus4 (SPI) processed data from OBDH to be transmitted
			radio_signals: requires bus access BUS7; -- bus7 (analog radio signal to antennas)
	end main_radio;

	device implementation main_radio.impl
		properties
			Data_Rate => 1 MBytesps; -- (max) (transmission)
			Physical::Voltage => 5.0 V; --(input)
	end main_radio.impl;

	---- Beacon radio
	--(Radio Module RF4463F30)
	device beacon_radio
		features
			mcu_data_in: requires bus access generalBUS; -- (SPI) internal connection: processed data from MCU to be transmitted
			beacon_message: requires bus access BUS7; -- bus7 (analog radio signal to antennas)
	end beacon_radio;

	device implementation beacon_radio.impl
				properties
			Data_Rate => 1 MBytesps; -- (max) (transmission)
			Physical::Voltage => 5.0 V; --(input)
	end beacon_radio.impl;

	---- BUSes
	bus generalBUS
		properties
			Allowed_Connection_Type => (Port_Connection, Data_Access_Connection);
	end generalBUS;
	-- BUS3: OBDH uC <-> TTC MCU
	bus BUS3_ttc extends generalBUS
		properties
			Data_Rate => 2000 bitsps; -- 2 kbps
	end BUS3_ttc;
	bus implementation BUS3_ttc.impl 
	end BUS3_ttc.impl;

	-- BUS4: OBDH <-> Main Radio
	bus BUS4_ttc extends generalBUS
		properties
			Data_Rate => 8000000 bitsps; -- 8Mbps
	end BUS4_ttc;
	bus implementation BUS4_ttc.impl 
	end BUS4_ttc.impl;


	-- BUS5: TTC MCU <-> Antenna MUC 2
	bus BUS5_ttc extends generalBUS
		properties
			Data_Rate => 100000 bitsps; -- 100 kbps
	end BUS5_ttc;
	bus implementation BUS5_ttc.impl 
	end BUS5_ttc.impl;

	-- BUS6: EPS <-> TTC MCU
	bus BUS6_ttc extends generalBUS
		properties
			Data_Rate => 9600 bitsps;
	end BUS6_ttc;
	bus implementation BUS6_ttc.impl 
	end BUS6_ttc.impl;

	-- BUS7: Main Radio and Beacon Radio <-> Antennas
	bus BUS7
		properties
			Allowed_Connection_Type => (Port_Connection);-- analog
	end BUS7;
	bus implementation BUS7.impl 
	end BUS7.impl;
	
	---- Headers
	device header
		features
			--~~~~~~~~~~PLACE HERE EXTERNAL BUS ACCESS (BUS5)~~~~~~~~~~
			bus_access: requires bus access BUS5_ttc;
	end header;
	device implementation header.impl
	end header.impl;
	
	device PC104
		features
			--~~~~~~~~~~PLACE HERE EXTERNAL BUS ACCESS (BUS3)~~~~~~~~~~
			--~~~~~~~~~~PLACE HERE EXTERNAL BUS ACCESS (BUS4)~~~~~~~~~~
			--~~~~~~~~~~PLACE HERE EXTERNAL BUS ACCESS (BUS6)~~~~~~~~~~
			bus_access3: requires bus access BUS3_ttc;
			bus_access4: requires bus access BUS4_ttc;
			bus_access6: requires bus access BUS6_ttc;
	end PC104;
	device implementation PC104.impl
	end PC104.impl;
	
	device debug_header
		features
			--~~~~~~~~~~PLACE HERE EXTERNAL BUS ACCESS (JTAG)~~~~~~~~~~
			bus_access: requires bus access generalBUS;
	end debug_header;
	device implementation debug_header.impl
	end debug_header.impl;
	
	---- Antenna
	device antenna
		features
			bus_access7: requires bus access BUS7;
	end Antenna;
	device implementation antenna.impl
	end antenna.impl;
end ttc;