package SAT_OBDH_System
public
	with SAT_OBDH_devices;
	with Buses::I2C;
	with Buses::SPI;
	with Buses::UART;
	with Buses::Analog;
	with SAT_OBDH_process;
	with Buses::Power;
	with SEI;

	system OBDH
		features
			BUS0: requires bus access Buses::I2C::I2C.impl;
			BUS1: requires bus access Buses::SPI::SPI.impl;
			BUS2: requires bus access Buses::I2C::I2C.impl;
			BUS3: requires bus access Buses::SPI::SPI.impl;
			BUS4: requires bus access Buses::SPI::SPI.impl;
			BUS8: requires bus access Buses::Analog::Analog.impl;
			BUS_JTAG: requires bus access Buses::UART::UART.impl;
			-- Power
			-- Power Buses
--			BUS_DC_OBDH_TTC: requires bus access Buses::Power::Power_V3DC_OBDH_TTC;
			BUS_DC_OBDH_TTC: requires bus access Buses::Power::Power_bus;
		properties
			SEI::GrossWeight => 0.046kg;
	end OBDH;

	system implementation OBDH.impl
		subcomponents
			----------- OBDH DEVICES -----------------------------------
			mcu_obdh: processor SAT_OBDH_devices::MSP430F6659.impl;
			sd_card: memory SAT_OBDH_devices::sd_card;
			imu_1: device SAT_OBDH_devices::MPU9250;
			imu_2: device SAT_OBDH_devices::BMX055;
			voltage_monitor: device SAT_OBDH_devices::voltage_monitor;
			current_driver_x: device SAT_OBDH_devices::DRV8833;
			current_driver_y: device SAT_OBDH_devices::DRV8833;
			current_driver_z: device SAT_OBDH_devices::DRV8833;
			current_sensor_board: device SAT_OBDH_devices::MAX9934TAUA;
			current_sensor_x: device SAT_OBDH_devices::MAX9934TAUA;
			current_sensor_y: device SAT_OBDH_devices::MAX9934TAUA;
			current_sensor_z: device SAT_OBDH_devices::MAX9934TAUA;
			voltage_sensor_board: device SAT_OBDH_devices::TLV341AIDBVR;
			OBDH_firmware: process SAT_OBDH_process::mcu.impl;

			-- BUSES
			-- Internal buses
			i2c_bus: bus Buses::I2C::I2C.impl;
			spi_bus: bus Buses::SPI::SPI.impl;
			
			

		connections
------ OBDH CONNECTIONS FROM POWER BUS
			Vp_2a: bus access BUS8 -> current_sensor_x.power_ADC;
			Vp_2b: bus access BUS8 -> current_sensor_y.power_ADC;
			Vp_2c: bus access BUS8 -> current_sensor_z.power_ADC;
			Vp_3a: bus access BUS_DC_OBDH_TTC -> current_sensor_x.power_3v3;
			Vp_3b: bus access BUS_DC_OBDH_TTC -> current_sensor_y.power_3v3;
			Vp_3c: bus access BUS_DC_OBDH_TTC -> current_sensor_z.power_3v3;
			Vp_4a: bus access BUS_DC_OBDH_TTC -> current_driver_x.power_3v3;
			Vp_4b: bus access BUS_DC_OBDH_TTC -> current_driver_y.power_3v3;
			Vp_4c: bus access BUS_DC_OBDH_TTC -> current_driver_z.power_3v3;
			Vp_5a: bus access current_driver_x.bus_ADC -> BUS8;
			Vp_5b: bus access current_driver_y.bus_ADC -> BUS8;
			Vp_5c: bus access current_driver_z.bus_ADC -> BUS8;
			Vi_1a: bus access BUS_DC_OBDH_TTC -> imu_1.power_3v3;
			Vi_1b: bus access BUS_DC_OBDH_TTC -> imu_2.power_3v3;
			Vm_1: bus access BUS_DC_OBDH_TTC -> mcu_obdh.power_V3;
			
			-- COM Device <-> Processor
			
			Vc_1a: bus access mcu_obdh.i2c_bus -> i2c_bus;
			Vc_1b: bus access imu_1.i2c_bus <-> i2c_bus;
			Vc_1c: bus access imu_2.i2c_bus <-> i2c_bus;
			Vc_2a: bus access sd_card.spi_bus <-> spi_bus;
			Vc_2b: bus access mcu_obdh.spi_bus <-> spi_bus;
			Vc_0: bus access  mcu_obdh.i2c_bus -> BUS0;
			Vc_1: bus access mcu_obdh.spi_bus -> BUS1;
			Vc_2: bus access  mcu_obdh.i2c_bus -> BUS2;
			Vc_3: bus access mcu_obdh.spi_bus -> BUS3;
			Vc_4: bus access mcu_obdh.spi_bus -> BUS4;
			Vc_8: bus access mcu_obdh.uart_bus -> BUS_JTAG;
			
			

			
			-- OBDH PROCESS
			-- Solar Panels Connections to current measurement
			C1_1c: port current_sensor_x.current_data -> OBDH_firmware.solar_panel_1_current;
			C1_2c: port current_sensor_y.current_data -> OBDH_firmware.solar_panel_2_current;
			C1_3c: port current_sensor_z.current_data -> OBDH_firmware.solar_panel_3_current;
			-- OBDH to current drive to magnetorques
			C2_1a: port OBDH_firmware.solar_panel_1_current_driver -> current_driver_x.signal_in;
			C2_2a: port OBDH_firmware.solar_panel_2_current_driver -> current_driver_y.signal_in;
			C2_3a: port OBDH_firmware.solar_panel_3_current_driver -> current_driver_z.signal_in;
			-- Supply sensing
			C4_1: port current_sensor_board.current_data -> OBDH_firmware.board_current;
			C4_2: port voltage_sensor_board.voltage_data -> OBDH_firmware.board_voltage;
			-- Voltage monitor
			C5_1: port OBDH_firmware.volt_monitor_out -> voltage_monitor.port_in;
			C5_2: port voltage_monitor.port_out -> OBDH_firmware.volt_monitor_in;
			-- IMU Data 
			C6_1: port imu_1.DOF6 -> OBDH_firmware.imu_1_data_in;
			C6_2: port imu_2.DOF6 -> OBDH_firmware.imu_2_data_in;

	end OBDH.impl;
			
			

	
end SAT_OBDH_System;